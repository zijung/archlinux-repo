# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: kirigaya <kirigaya@mkacg.com>

pkgname=deepin-topbar
pkgver=0.4.6
pkgrel=1
pkgdesc="This is topbar for Deepin"
arch=('i686' 'x86_64')
url="https://github.com/kirigayakazushin/deepin-topbar"
license=('GPL')
depends=('deepin-qt5integration' 'deepin-qt-dbus-factory')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kirigayakazushin/deepin-topbar/archive/$pkgver.tar.gz")
sha512sums=('b49fa1d4dbc653703dfebdf141939a102888f7e299f9843618a742e9a9b8b22a6687ede9d5051ceb802f5527137c6df2bd52f6f7ba95c98ffd5f6499b1cdcd7d')

build() {
  cd $pkgname-$pkgver
  qmake QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
